apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-allopen'
apply plugin: 'jacoco'
apply plugin: 'com.google.firebase.firebase-perf'

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.dependency_injection.sample"
        multiDexEnabled true
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "0.8.18"
        signingConfig signingConfigs.debug
        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath false
                arguments = ["room.schemaLocation"  : "$projectDir/schemas".toString(),
                             "room.incremental"     : "true",
                             "room.expandProjection": "true"]
            }
        }

    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${variant.name}-${variant.versionCode}-${variant.versionName}.apk"
        }
    }

    android {
        sourceSets {
            String sharedTestDir = 'src/sharedTest/java'
            test {
                java.srcDir sharedTestDir
            }
            androidTest {
                java.srcDir sharedTestDir
            }
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled !project.hasProperty('android.injected.invoked.from.ide')
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            multiDexKeepFile file('multidex-config.txt')
            multiDexKeepProguard file('multidex-config.pro')
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        androidTest.kotlin.srcDirs += "src/test-common/kotlin"
        test.kotlin.srcDirs += "src/test-common/kotlin"
        main.java.srcDirs += 'src/main/kotlin'
    }

    flavorDimensions "default"

    productFlavors {
        prod {
            dimension "default"
            manifestPlaceholders = [appNameGradle: "Shareable Asset"]
            buildConfigField "String", "apiServer", "\"https://google.com/api/\""
        }

        stg {
            dimension "default"
            applicationIdSuffix = ".stg"
            manifestPlaceholders = [appNameGradle: "Shareable Asset"]
            buildConfigField "String", "apiServer", "\"https://stg.google.com/api/\""
        }

        dev {
            dimension "default"
            applicationIdSuffix = ".dev"
            manifestPlaceholders = [appNameGradle: "SADev"]
            buildConfigField "String", "apiServer", "\"https://dev.google.com/api/\""
        }
    }

    testOptions.unitTests.returnDefaultValues = true
    testOptions.unitTests.all {

        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    buildToolsVersion = '29.0.2'

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8

        kotlinOptions {
            suppressWarnings = true
            jvmTarget = '1.8'
            apiVersion = '1.4'
            languageVersion = '1.4'
        }
    }

    compileOptions {
        // Flag to enable support for the new language APIs
        // Sets Java compatibility to Java 8
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

}

jacoco {
    toolVersion = "0.7.4+"
}

kapt {
    useBuildCache = true
}

androidExtensions {
    experimental = true
}

repositories {
    mavenCentral()
    jcenter()

    maven {
        url 'https://maven.google.com/'
        name 'Google'
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    implementation "androidx.core:core-ktx:$androidx_core_ktx_version"
    implementation "androidx.appcompat:appcompat:$androidx_appcompat_version"
    implementation "androidx.recyclerview:recyclerview:$androidx_version"
    implementation "androidx.cardview:cardview:$androidx_cardview_version"
    implementation "androidx.browser:browser:$androidx_browser_version"
    implementation "androidx.annotation:annotation:$androidx_version"
    implementation "androidx.multidex:multidex:$androidx_multidex_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraint_layout_version"
    // ViewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-extensions:$arch_lifecycle_version"
    // alternatively, just ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel:$arch_lifecycle_version"
    // alternatively, just LiveData
    implementation "androidx.lifecycle:lifecycle-livedata:$arch_livecycle_livedata_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$arch_livedata_ktx"
    // Lifecycles only (no ViewModel or LiveData)
    implementation "androidx.lifecycle:lifecycle-runtime:$arch_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$arch_lifecycle_ktx"
    implementation "androidx.lifecycle:lifecycle-common-java8:$arch_lifecycle_version"
    implementation "androidx.legacy:legacy-support-v4:1.0.0"

    // Room (use 1.1.0-alpha1 for latest alpha)
    implementation "androidx.room:room-runtime:$arch_room_version"
    implementation "androidx.room:room-ktx:$arch_room_ktx"
    implementation "androidx.appcompat:appcompat:$androidx_appcompat_version"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "androidx.work:work-runtime-ktx:$arch_workmanager_version"
    implementation "androidx.work:work-rxjava2:$arch_workmanager_version"
    implementation "androidx.work:work-gcm:$arch_workmanager_version"
    annotationProcessor "androidx.room:room-compiler:$arch_room_version"
    kapt "androidx.room:room-compiler:$arch_room_version"
    // Paging
    implementation "androidx.paging:paging-runtime:$arch_paging_runtime_version"
    // Test helpers for LiveData
    testImplementation "androidx.arch.core:core-testing:$arch_version"
    // Test helpers for Room
    testImplementation "androidx.room:room-testing:$arch_room_version"
    // ReactiveStreams support for LiveData
    implementation "androidx.lifecycle:lifecycle-reactivestreams:$arch_lifecycle_version"

    // CameraX core library
    implementation "androidx.camera:camera-core:$androidx_camerax_version"
    implementation "androidx.camera:camera-camera2:$androidx_camerax_version"
    // If you want to use the CameraX View class
    implementation "androidx.camera:camera-view:1.0.0-alpha07"
    // If you want to use the CameraX Extensions library
    implementation "androidx.camera:camera-extensions:1.0.0-alpha07"
    // If you want to use the CameraX Lifecycle library
    implementation "androidx.camera:camera-lifecycle:1.0.0-alpha10"

    //noinspection GradleDependency
    implementation "com.google.android.material:material:$design_support_version"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.3'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.3'

    implementation "com.squareup.retrofit2:retrofit:2.7.1"
    implementation "com.squareup.retrofit2:converter-gson:2.7.1"
    implementation "com.squareup.okhttp3:okhttp:4.3.1"
    implementation "com.squareup.okhttp3:okhttp-urlconnection:4.3.1"
    implementation "com.squareup.okhttp3:logging-interceptor:4.3.1"

    implementation "com.google.dagger:dagger:2.25.4"
    implementation "com.google.dagger:dagger-android:2.25.4"
    implementation "com.google.dagger:dagger-android-support:2.25.4"
    annotationProcessor "com.google.dagger:dagger-android-processor:2.25.4"
    kapt "com.google.dagger:dagger-compiler:2.25.4"
    kapt "com.google.dagger:dagger-android-processor:2.25.4"

    implementation 'com.google.firebase:firebase-core:17.2.2'
    implementation 'com.google.firebase:firebase-analytics:17.2.2'
    implementation 'com.google.firebase:firebase-messaging:20.1.0'
    implementation 'com.google.firebase:firebase-dynamic-links:19.0.0'
    implementation 'com.google.firebase:firebase-perf:19.0.5'
    implementation 'com.google.firebase:firebase-config:19.1.1'

    // Glide(Image library)
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    implementation 'com.github.bumptech.glide:annotations:4.11.0'
    kapt 'com.github.bumptech.glide:compiler:4.11.0'

    implementation "com.jakewharton.timber:timber:$timber_version"

    implementation 'com.github.franmontiel:PersistentCookieJar:v1.0.1'

    // Joda Time
    implementation 'net.danlew:android.joda:2.10.3'
    testImplementation 'joda-time:joda-time:2.10.5'

    // Json Logging
    implementation 'com.orhanobut:logger:2.2.0'

    // Rx
    implementation "io.reactivex.rxjava2:rxjava:2.2.17"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"

    // Rx Utils
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.2.0'

    // Debuging Network & Resources
    implementation 'com.facebook.stetho:stetho:1.5.1'
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.1'

    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

task fullCoverageReport(type: JacocoReport) {
    dependsOn 'createDebugCoverageReport'
    dependsOn 'testDebugUnitTest'
    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*',
                      '**/*Test*.*', 'android/**/*.*',
                      '**/*_MembersInjector.class',
                      '**/Dagger*Component.class',
                      '**/Dagger*Component$Builder.class',
                      '**/*_*Factory.class',
                      '**/*ComponentImpl.class',
                      '**/*SubComponentBuilder.class']
    def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/debug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/kotlin"

    getClassDirectories().setFrom(([mainSrc]))
    getClassDirectories().setFrom(files([debugTree]))
    getExecutionData().setFrom(fileTree(dir: "$buildDir", includes: [
            "jacoco/testDebugUnitTest.exec",
            "outputs/code-coverage/connected/*coverage.ec"
    ]))
}
